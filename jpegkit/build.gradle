plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'com.jfrog.bintray' version '1.7.3'
    id 'com.github.dcendents.android-maven' version '1.5'
}

android {
    compileSdkVersion = 28
    buildToolsVersion = '28.0.3'
    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 28
        externalNativeBuild {
            cmake {
                cppFlags ''
            }
        }
    }
    externalNativeBuild {
        cmake {
            path 'CMakeLists.txt'
        }
    }
}

repositories {
    jcenter()
    google()
}

dependencies {
    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk7:1.2.50'
    implementation 'com.android.support:exifinterface:28.0.0-alpha3'
}

ext.getBintrayUser = { ->
    if (System.getenv('BINTRAY_USER')) {
        return System.getenv('BINTRAY_USER')
    }

    Properties properties = new Properties()
    properties.load(rootProject.file('local.properties').newDataInputStream())
    return properties.getProperty('bintray.user')
}

ext.getBintrayKey = { ->
    if (System.getenv('BINTRAY_KEY')) {
        return System.getenv('BINTRAY_KEY')
    }

    Properties properties = new Properties()
    properties.load(rootProject.file('local.properties').newDataInputStream())
    return properties.getProperty('bintray.key')
}


task cleanDeployment {
    doLast {
        logger.lifecycle('JpegKit Deployment: Cleaning...')

        logger.lifecycle('Deleting: ' + project.buildDir)
        delete project.buildDir
    }
}

task buildDeployment {
    shouldRunAfter(cleanDeployment)
    finalizedBy assemble

    doFirst {
        android.variantFilter { variant ->
            if (variant.buildType.name == 'debug') {
                variant.setIgnore(true)
            }
        }
    }
}

task deploySnapshot {
    shouldRunAfter(buildDeployment)

    dependsOn cleanDeployment
    dependsOn buildDeployment
    finalizedBy bintrayUpload

    doLast {
        bintrayUpload.setVersionName(bintrayUpload.getVersionName())
        bintrayUpload.setUserOrg('camerakit')
        bintrayUpload.setRepoName('jpegkit-android')
        bintrayUpload.setOverride(true)
        bintrayUpload.setPublish(true)

        logger.lifecycle('Deploying version ' + bintrayUpload.getVersionName() + ' in ' + bintrayUpload.getRepoName())
    }
}

task deployRelease {
    shouldRunAfter(buildDeployment)

    dependsOn cleanDeployment
    dependsOn buildDeployment
    finalizedBy bintrayUpload

    doLast {
        bintrayUpload.setUserOrg('camerakit')
        bintrayUpload.setRepoName('jpegkit-android')

        logger.lifecycle('Deploying version ' + bintrayUpload.getVersionName() + ' in ' + bintrayUpload.getRepoName())
    }
}

group = 'com.camerakit'
version = '0.2.2'

install {
    repositories.mavenInstaller {
        pom.project {
            name 'jpegkit-android'
            groupId 'com.camerakit'
            artifactId 'jpegkit'
            packaging 'aar'
        }
    }
}

bintray {
    user = getBintrayUser()
    key = getBintrayKey()
    configurations = ['archives']
    pkg {
        name = 'jpegkit-android'
        vcsUrl = 'https://github.com/CameraKit/jpegkit-android.git'
        licenses = ['MIT']
        version {
            name = project.version
            released = new Date()
        }
    }
}
